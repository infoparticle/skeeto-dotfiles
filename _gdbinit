set confirm off
set pagination off
set print pretty on
set verbose off
set history save on
set prompt \001\033[1;93m\002gdb> \001\033[0m\002
set disassembly-flavor intel
set auto-load safe-path /
alias g = start

## Skip over the implementation's stack frames on abort/etc.
if !$_isvoid($_caller_matches)
    define hook-stop
        if $_caller_matches("^__")
            while $_caller_matches("^__")
                up-silently
            end
            up-silently
        end
    end
end

define crt
    define _upone
        if $once
            up
            set $once = 0
        end
    end
    define hook-run
        set $once = 1
        tbreak abort
        commands
            silent
            _upone
        end
        tbreak _assert
        commands
            silent
            _upone
        end
    end
    define g
        tbreak main
        run
    end
end
document crt
Configure for an EXE linked against msvcrt.dll
Does not handle -municode (wmain, wWinMain, _wassert)
end

define nocrt
    define g
        tbreak mainCRTStartup
        tbreak WinMainCRTStartup
        run
    end
end
document nocrt
Configure for an EXE not linked against a CRT
end
